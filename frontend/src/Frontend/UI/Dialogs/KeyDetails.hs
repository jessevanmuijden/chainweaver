{-# LANGUAGE ConstraintKinds #-}
{-# LANGUAGE CPP #-}
{-# LANGUAGE DataKinds #-}
{-# LANGUAGE RecursiveDo #-}
{-# LANGUAGE TypeApplications #-}
-- | Dialog for viewing the details of a key.
-- Copyright   :  (C) 2020 Kadena
-- License     :  BSD-style (see the file LICENSE)
module Frontend.UI.Dialogs.KeyDetails
  ( uiKeyDetails
  ) where

------------------------------------------------------------------------------
#if !defined(ghcjs_HOST_OS)
import qualified Codec.QRCode as QR
import qualified Codec.QRCode.JuicyPixels as QR
#endif
import           Control.Error
import           Control.Lens
import           Control.Monad (join)
import qualified Data.Aeson as A
import           Data.Aeson.Parser.Internal (jsonEOF')
import           Data.Attoparsec.ByteString
import           Data.Bifunctor (first)
import qualified Data.ByteString.Lazy as LB
import           Data.Functor (void)
import qualified Data.Text as T
import qualified Data.Text.Encoding as T
import qualified Data.Text.Lazy as LT
import qualified Data.IntMap as IntMap
import qualified Data.Yaml as Y
import           Pact.Types.ChainMeta (PublicMeta)
import           Pact.Types.Command
import           Pact.Types.Hash (hash, toUntypedHash, unHash)
import           Pact.Types.SigData
import           Pact.Types.Util             (decodeBase64UrlUnpadded)
------------------------------------------------------------------------------
import           Reflex
import           Reflex.Dom hiding (Key)
------------------------------------------------------------------------------
import           Frontend.Crypto.Class
import           Frontend.Foundation
import           Frontend.UI.Modal
import           Frontend.UI.Transfer
import           Frontend.UI.Widgets
import           Frontend.UI.Widgets.Helpers (dialogSectionHeading)
import           Frontend.Wallet
------------------------------------------------------------------------------

data DataToBeSigned
  = JsonSigData (SigData T.Text)
  | JsonPayload (Payload PublicMeta T.Text)
  | YamlSigData (SigData T.Text)
  | ErrorString String
  | EmptyString

uiKeyDetails
  :: ( Monoid mConf
     , HasCrypto key m
     , MonadWidget t m
     )
  => IntMap.Key
  -> Key key
  -> Event t ()
  -> m (mConf, Event t ())
uiKeyDetails _keyIndex key _onCloseExternal = mdo
  onClose <- modalHeader $ text "Key Details"
  let withSecretKey f = case _keyPair_privateKey . _key_pair $ key of
        Nothing -> text "Public key does not have a matching secret key - use a keypair generated by Chainweaver instead"
        Just x -> f x

      publicKey = keyToText $ _keyPair_publicKey $ _key_pair key

      parseBytes "" = EmptyString
      parseBytes bytes =
        -- Parse the JSON, and consume all the input
        case parseOnly jsonEOF' bytes of
            -- If we do receive JSON, it can be of two types: either a SigData Text,
            -- or a Payload PublicMeta Text
          Right val -> case A.fromJSON val of
            A.Success sigData -> JsonSigData sigData
            A.Error _ -> case A.fromJSON val of
              A.Success payload -> JsonPayload payload
              A.Error errorStr -> ErrorString errorStr
          Left _ -> case Y.decodeEither' bytes of
            Right sigData -> YamlSigData sigData
            -- TODO: Find a better way to convert this to a string
            Left excp -> ErrorString $ show excp

      -- Create an input textbox, along with an output textbox
      -- The output textbox will also have an associated copy button,
      -- whenever it is non-empty.
      inputOutputBoxesWithCopy headerText =
        withSecretKey $ \pk ->
          divClass "group" $ do
            txt <- fmap value $ mkLabeledClsInput False headerText $ \cls -> uiTextAreaElement $ def
              & initialAttributes .~ "class" =: renderClass cls

            let
              signingDataEv = (parseBytes . T.encodeUtf8) <$> updated txt

              withHeader h t = fmap ((,) h) t

              parsePayload :: T.Text -> Either T.Text (Payload PublicMeta T.Text)
              parsePayload cmdText =
                first (const "Invalid cmd field inside SigData.") $
                  A.eitherDecodeStrict (T.encodeUtf8 cmdText)

              matchHash oldHash textToBeHashed =
                if oldHash == hash (T.encodeUtf8 textToBeHashed)
                  then Right oldHash
                  else Left "Hash does not match the payload."

              signHashWithKey h = keyToText <$> cryptoSign (unHash $ toUntypedHash h) pk

              signHashIfSignerExists pubKey hashToSign payload =
                if pubKey `elem` map _siPubKey (_pSigners payload)
                  then Right <$> signHashWithKey hashToSign
                  else pure $ Left "You are not one of the signers."

              matchHashAndParsePayload h cmd = do
                verifiedHash <- matchHash h cmd
                payload <- parsePayload cmd
                pure (verifiedHash, payload)

            ((), headerAndHashEv) <- runWithReplace blank $ ffor signingDataEv $ \case
              JsonSigData (SigData sdHash _ (Just cmd)) -> do
                withHeader "Signature" $ do
                  fmap join
                    $ mapM (uncurry $ signHashIfSignerExists publicKey)
                    $ matchHashAndParsePayload sdHash cmd
              JsonPayload payload -> do
                let
                  unsignedCmd = payloadToCommand payload
                  signCommand cmd signedHash = cmd  & cmdSigs .~ [signedHash]
                withHeader "Signed Command"
                  $ fmap
                    ( T.decodeUtf8
                    . LB.toStrict
                    . A.encode
                    . signCommand unsignedCmd
                    . UserSig
                    )
                  <$> signHashIfSignerExists publicKey (_cmdHash unsignedCmd) payload
              YamlSigData sigData@(SigData sdHash sigList (Just cmd)) -> do
                let
                  -- TODO: Optimize this
                  allSignatures signature payload = map (\s ->
                    let
                      pKey = PublicKeyHex $ _siPubKey s
                      myKey = PublicKeyHex publicKey
                    in
                      ( pKey
                      , if pKey == myKey
                          then Just $ UserSig signature
                          else join $ lookup pKey sigList
                      )
                    ) $ _pSigners payload
                  updateSigData sd allSigs = sd { _sigDataSigs = allSigs}
                withHeader "Signed SigData" $ do
                  let hashAndPayload = matchHashAndParsePayload sdHash cmd
                  signature <- join <$> mapM (uncurry $ signHashIfSignerExists publicKey) hashAndPayload
                  pure $ fmap (T.decodeUtf8 . Y.encode . updateSigData sigData) $ allSignatures <$> signature <*> (snd <$> hashAndPayload)
              ErrorString errorStr -> withHeader "Signature" $ pure $ Left $ T.pack errorStr
              EmptyString -> withHeader "Signature" $ pure $ Left ""
              _ -> withHeader "Signature" $ pure $ Left "Could not parse data."

            let (headerEv, hashEv) = splitE headerAndHashEv

            widgetHold_ blank $ ffor hashEv $ \case
              Right _ -> blank
              Left "" -> blank
              Left errorStr -> elClass "p" "error_inline" $ text errorStr

            headerDyn <- holdDyn "Signature" headerEv
            void $ mkLabeledClsInput False headerDyn $ \cls -> uiTextAreaElement $ def
              & initialAttributes .~ mconcat
                [ "class" =: renderClass cls
                , "disabled" =: ""
                , "placeholder" =: "Enter some text in the above field"
                ]
              & textAreaElementConfig_setValue .~ fmap fold hashEv

  divClass "modal__main key-details" $ do
    dialogSectionHeading mempty "Public Key"
    -- We need the `group__header` class to provide some margin after the
    -- Public key input box.
    divClass "group group__header" $ do
      void $ uiInputElement $ def
        & inputElementConfig_initialValue .~ publicKey
        & initialAttributes <>~ ("disabled" =: "true" <> "class" =: " key-details__pubkey input labeled-input__input")

    inputOutputBoxesWithCopy "Data to sign ( JSON/YAML )"

    void $ accordionItemWithClick False mempty (accordionHeaderBtn "Advanced") $ withSecretKey $ \pk -> do
      divClass "group" $ do
        txt <- fmap value $ mkLabeledClsInput False "Data to sign (Base64Url Unpadded)" $ \cls -> uiTextAreaElement $ def
          & initialAttributes .~ "class" =: renderClass cls

        ((), sigEv) <- runWithReplace blank $ ffor (fmapMaybe (hush . decodeBase64UrlUnpadded . T.encodeUtf8) $ updated txt) $ \case
          "" -> pure Nothing
          b -> Just . keyToText <$> cryptoSign b pk

        sig <- maybeDyn =<< holdDyn Nothing sigEv


        void $ mkLabeledClsInput False "Signature" $ \cls -> uiTextAreaElement $ def
          & initialAttributes .~ mconcat
            [ "class" =: renderClass cls
            , "disabled" =: ""
            , "placeholder" =: "Enter some text in the above field"
            ]
          & textAreaElementConfig_setValue .~ ffor sigEv fold

        dyn_ $ ffor sig $ \case
          Nothing -> blank
          Just sig' -> do
            uiDetailsCopyButton $ current sig'
#if !defined(ghcjs_HOST_OS)
            let qrImage = QR.encodeText (QR.defaultQRCodeOptions QR.L) QR.Iso8859_1OrUtf8WithECI <$> sig'
                img = maybe "Error creating QR code" (QR.toPngDataUrlT 4 6) <$> qrImage
            el "br" blank
            elDynAttr "img" (("src" =:) . LT.toStrict <$> img) blank
#endif

  modalFooter $ do
    onDone <- confirmButton def "Done"

    let done = leftmost [onClose, onDone]

    pure (mempty, done)
